
#######
# Env #
#######
export PATH="${HOME}/.bin:${PATH}"
[[ -e ~/.gnubinrc ]] && source ~/.gnubinrc

###########
# History #
###########
# „Éá„Ç£„É¨„ÇØ„Éà„É™Â±•Ê≠¥
if [[ -n $(echo ${^fpath}/chpwd_recent_dirs(N)) && -n $(echo ${^fpath}/cdr(N)) ]]; then
	# cdr, add-zsh-hook „ÇíÊúâÂäπ„Å´„Åô„Çã
	autoload -Uz chpwd_recent_dirs cdr add-zsh-hook
	add-zsh-hook chpwd chpwd_recent_dirs

	# cdr „ÅÆË®≠ÂÆö
	zstyle ':completion:*' recent-dirs-insert both
	zstyle ':chpwd:*' recent-dirs-max 20000
	zstyle ':chpwd:*' recent-dirs-default true
	zstyle ':chpwd:*' recent-dirs-pushd true
fi

# „Ç≥„Éû„É≥„ÉâÂ±•Ê≠¥
HISTFILE=~/.zsh_history
HISTSIZE=10000000
SAVEHIST=10000000
setopt hist_ignore_all_dups # ignore duplication command history list
setopt hist_ignore_space    # ignore if start with white space
setopt hist_save_no_dups    # ignore duplicated
setopt hist_reduce_blanks   # ignore white spaces
setopt share_history        # share command history data
setopt hist_expand          # Ë£úÂÆåÊôÇ„Å´„Éí„Çπ„Éà„É™„ÇíËá™ÂãïÁöÑ„Å´Â±ïÈñã
setopt hist_no_store        # history„Ç≥„Éû„É≥„Éâ„ÅØÂ±•Ê≠¥„Å´ÁôªÈå≤„Åó„Å™„ÅÑ
setopt extended_history     # Â±•Ê≠¥„Å´ÊôÇÂàª„ÇíËøΩÂä†


#############
# Key Binds #
#############
# EmacsÈ¢®„Ç≠„Éº„Éê„Ç§„É≥„Éâ
bindkey -e

# „Ç≥„Éû„É≥„ÉâÂ±•Ê≠¥Ê§úÁ¥¢„ÇíC-p„Å®C-n„Å´Ââ≤„ÇäÂΩì„Å¶„Çã
autoload -Uz history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^P" history-beginning-search-backward-end
bindkey "^N" history-beginning-search-forward-end
ZSH_AUTOSUGGEST_CLEAR_WIDGETS+=(history-beginning-search-backward-end history-beginning-search-forward-end) # https://github.com/zsh-users/zsh-autosuggestions/issues/619#issuecomment-904193190

############
# Zsh Misc #
############
# „Ç≥„Éû„É≥„ÉâÈñ¢ÈÄ£etc
setopt auto_cd           # „Éá„Ç£„É¨„ÇØ„Éà„É™Âêç„ÇíÂÖ•„Çå„Çã„Å®cd
setopt auto_pushd        # tab„Ç≠„Éº„ÅßcdÂ±•Ê≠¥
setopt correct           # „Ç≥„Éû„É≥„ÉâË£úÂÆå
setopt list_packed       # Ë£úÂÆå„É™„Çπ„Éà„ÇíÂúßÁ∏ÆË°®Á§∫
setopt nolistbeep        # „Éì„Éº„ÉóÈü≥„ÇíÊÆ∫„Åô
setopt multios           # „Éû„É´„ÉÅ„É™„ÉÄ„Ç§„É¨„ÇØ„Éà(„Å°„Çá„Å£„Å®Âç±Èô∫)
setopt magic_equal_subst # = ‰ª•Èôç„Åß„ÇÇË£úÂÆå„Åß„Åç„Çã„Çà„ÅÜ„Å´„Åô„Çã( --prefix=/usr Á≠â„ÅÆÂ†¥Âêà)
setopt list_types        # Ë£úÂÆåÂÄôË£ú‰∏ÄË¶ß„Åß„Éï„Ç°„Ç§„É´„ÅÆÁ®ÆÂà•„ÇíË≠òÂà•„Éû„Éº„ÇØË°®Á§∫(ls -F „ÅÆË®òÂè∑)
unsetopt no_clobber      # „É™„ÉÄ„Ç§„É¨„ÇØ„Éà„Åß‰∏äÊõ∏„Åç„ÇíË®±ÂèØ

#######################
# Init *env, Homebrew #
#######################
# Homebrew
[[ -d /opt/homebrew ]] && eval "$(/opt/homebrew/bin/brew shellenv)"
[[ -d /home/linuxbrew ]] && eval $(/home/linuxbrew/.linuxbrew/bin/brew shellenv)

[[ -d $HOMEBREW_PREFIX ]] && {
	eval "$(starship init zsh)"
	eval "$(mise activate zsh)"
}

#################
# Auto-Complete #
#################
# for brew zsh-completions #
[[ $(type brew) ]] && {
	chmod -R go-w "$(brew --prefix)/share"
	FPATH=$(brew --prefix)/share/zsh-completions:$FPATH
}
autoload -Uz bashcompinit && bashcompinit
autoload -Uz compinit -u && compinit
compinit

# Ë£úÂÆå„ÅÆÊôÇ„Å´Â§ßÊñáÂ≠óÂ∞èÊñáÂ≠ó„ÇíÂå∫Âà•„Åó„Å™„ÅÑ (‰ΩÜ„Åó„ÄÅÂ§ßÊñáÂ≠ó„ÇíÊâì„Å£„ÅüÂ†¥Âêà„ÅØÂ∞èÊñáÂ≠ó„Å´Â§âÊèõ„Åó„Å™„ÅÑ)
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

##############
# Appearance #
##############
# About prompt, please edit ~/.config/starship.toml

########
# tmux #
########
#tmux
alias tm='tmux'
alias ta='tmux attach-session'
alias tl='tmux list-session'

[[ $SHLVL = 1 ]] && tmux

# TMUX „ÅÆ„Ç¶„Ç£„É≥„Éâ„Ç¶Âêç„Çí„Ç´„É¨„É≥„Éà„Éá„Ç£„É¨„ÇØ„Éà„É™„Å´
show-current-dir-as-window-name() {
	if [ $SHLVL -gt 1 ];then
		tmux set-window-option window-status-format "[${PWD:t}]" > /dev/null
		tmux set-window-option window-status-current-format "#[fg=colour16,bg=colour220,bold][${PWD:t}]" > /dev/null
	fi
}
show-current-dir-as-window-name
add-zsh-hook chpwd show-current-dir-as-window-name


# uv
[[ $(which uv) && $? == 0 ]] && eval "$(uv generate-shell-completion zsh)"
[[ $(which uvx) && $? == 0 ]] && eval "$(uvx --generate-shell-completion zsh)"

###########
# sheldon #
###########
eval "$(sheldon source)"
# Please edit ~/.config/sheldon/plugins.toml


#######
# fzf #
#######
export FZF_DEFAULT_OPTS=" -e \
 --bind=ctrl-k:kill-line,ctrl-v:page-down,alt-v:page-up,change:top \
 --reverse \
 --no-scrollbar \
 --prompt='üîç ' --pointer='üëâ' \
 --color=hl:red,hl+:red,bg+:239"

# Ctrl-x -> Ctrl-f : „Éá„Ç£„É¨„ÇØ„Éà„É™„ÅÆÁßªÂãïÂ±•Ê≠¥„ÇíË°®Á§∫
function fzf-select-cdr() {
	local -r selected_dir=$(cdr -l |  awk '{print $2}' | fzf --no-sort)
	if [[ -n "$selected_dir" ]]; then
		BUFFER="cd ${selected_dir}"
		zle accept-line
	fi
	zle reset-prompt
}
zle -N fzf-select-cdr
bindkey '^x^f' fzf-select-cdr

# Ctrl-r : „Ç≥„Éû„É≥„Éâ„ÅÆÂÆüË°åÂ±•Ê≠¥„ÇíË°®Á§∫
function fzf-select-history() {
	BUFFER=$(fc -R .zsh_history && history -i -n 1 | fzf --query "$LBUFFER" --tac --no-sort | sed -E 's/[0-9]{4}-[0-9]{2}-[0-9]{2} [0-9]{2}:[0-9]{2}\s+//')
	CURSOR=$#BUFFER
	zle reset-prompt
}
zle -N fzf-select-history
bindkey '^r' fzf-select-history

# Ctrl+x -> Ctrl+g : gcloud config configuration „ÅÆÂàá„ÇäÊõø„Åà
function fzf-select-gcloud-config(){
	which gcloud 2>&1 > /dev/null
	[[ $? -ne 0 ]] && return;

	local config_name=$(gcloud config configurations list | tail -n +2 | fzf | awk '{print $1}')
	if [ ! -z $config_name ]; then
		BUFFER="gcloud config configurations activate '${config_name}'"
		CURSOR="${#BUFFER}"
	else
		BUFFER=""
		CURSOR=0
	fi
	zle -R -c
}
zle -N fzf-select-gcloud-config
bindkey '^x^g' fzf-select-gcloud-config


# Ctrl+x -> Ctrl+b : git branch „ÅÆÂàá„ÇäÊõø„Åà
function fzf-select-git-branch(){
	git status 2>&1 > /dev/null
	[[ $? -ne 0 ]] && return;

	local branch_name=$(git branch --list | cut -c 3- | fzf --preview "git log --graph --pretty='format:%as %aN %d %s' {}" --preview-window down,70%,nowrap)
	if [ ! -z $branch_name ]; then
		BUFFER="git checkout '${branch_name}'"
		CURSOR="${#BUFFER}"
	else
		BUFFER=""
		CURSOR=0
	fi
	zle -R -c
}
zle -N fzf-select-git-branch
bindkey '^x^b' fzf-select-git-branch


##############################
# Overwrite Command settings #
##############################
# ls
alias ll='ls -l'
alias la='ls -a'
alias lla='ls -la'
alias lal='ls -al'


# colors
[ -x /usr/bin/dircolors ] && { # Ëâ≤„Çí‰ªò„Åë„Çã
    eval "`dircolors -b`"
    alias ls='ls --color=auto'
		export LS_COLORS='di=34:ln=35:so=32:pi=33:ex=31:bd=46;34:cd=43;34:su=41;30:sg=46;30:tw=42;30:ow=43;30'
		zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}

		alias grep='grep --color=auto'
    alias fgrep='fgrep --color=auto'
    alias egrep='egrep --color=auto'
}

# pagers
export MANPAGER="/usr/bin/less -is"
[[ -x $(command -v bat) ]] && {
	export MANPAGER="sh -c 'col -bx | bat -l man -p'"
	export GIT_PAGER="bat -p"

	# cat to bat
	alias cat='bat -pp'
}

#„ÇØ„É™„ÉÉ„Éó„Éú„Éº„Éâ
alias clip='clip.exe' # for wsl
# alias clip='xsel --clipboard'


##################################
# „É≠„Éº„Ç´„É´Ë®≠ÂÆö„Éï„Ç°„Ç§„É´„ÇíË™≠„ÅøËæº„ÇÄ #
##################################
[ -f ~/.zshrc.mine ] && source ~/.zshrc.mine

